plugins {
    id 'com.google.protobuf' version '0.9.3'
}
apply plugin: "java"

apply from: "${rootDir}/shared/common.gradle"

dependencies {
    implementation wpilibTools.deps.wpilibJava("wpimath")
    implementation wpilibTools.deps.wpilibJava("apriltag")
    implementation "org.ejml:ejml-simple:0.42"

    // Junit
    testImplementation("org.junit.jupiter:junit-jupiter-api:5.8.2")
    testImplementation("org.junit.jupiter:junit-jupiter-params:5.8.2")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.8.2")
}

sourceSets {
    main {
        java {
            // TODO this is a hack! I think! Why are we generating wpilib quickbufs anyways?
            srcDirs 'build/generated/source/proto/main/quickbuf/org'
            exclude 'build/generated/source/proto/main/quickbuf/edu/wpi/**'
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}
tasks.withType(Javadoc) {
    options.encoding = 'UTF-8'
}

java {
    withJavadocJar()
    withSourcesJar()
}

test {
    useJUnitPlatform()
}


protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.21.12'
    }
    plugins {
        quickbuf {
            artifact = 'us.hebi.quickbuf:protoc-gen-quickbuf:1.3.2'
        }
    }
    generateProtoTasks {
        all().configureEach { task ->
            task.builtins {
                cpp {}
                remove java
            }
            task.plugins {
                quickbuf {
                    option "gen_descriptors=true"
                }
            }
        }
    }
}

apply from: "publish.gradle"
